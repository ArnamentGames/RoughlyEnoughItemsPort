plugins {
    id 'fabric-loom' version '0.2.4-SNAPSHOT'
    id 'maven-publish'
    id 'net.minecrell.licenser' version '0.4.1'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "RoughlyEnoughItems"
group = "me.shedaniel"

def ENV = System.getenv()
version = project.mod_version + "+" + (ENV.BUILD_NUMBER ? ("build." + ENV.BUILD_NUMBER) : "local")

def includeDep = true

minecraft {
}

license {
    header rootProject.file('HEADER')
    include '**/*.java'
    exclude '**/Scissors.java'
}

repositories {
    maven { url "https://tehnut.info/maven/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://minecraft.curseforge.com/api/maven" }
}

processResources {
    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
    inputs.property "version", project.version
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_version}"
    modApi "net.fabricmc:fabric-loader:${project.fabricloader_version}"
    modApi "net.fabricmc.fabric-api:fabric-keybindings-v0:${project.fabric_keybindings}"
    modApi "net.fabricmc.fabric-api:fabric-api-base:${project.fabric_api_base}"
    modApi "net.fabricmc.fabric-api:fabric-networking-v0:${project.fabric_networking}"
    modApi "net.fabricmc.fabric-api:fabric-resource-loader-v0:${project.fabric_resource_loader}"
    modApi "cloth:ClothEvents:${cloth_events_version}"
    modImplementation "cloth-config:ClothConfig2:${cloth_config_version}"
    modApi "blue.endless:jankson:${project.jankson_version}"
    if (includeDep) {
        include "net.fabricmc.fabric-api:fabric-keybindings-v0:${project.fabric_keybindings}"
        include "net.fabricmc.fabric-api:fabric-api-base:${project.fabric_api_base}"
        include "net.fabricmc.fabric-api:fabric-networking-v0:${project.fabric_networking}"
        include "net.fabricmc.fabric-api:fabric-resource-loader-v0:${project.fabric_resource_loader}"
        include "cloth:ClothEvents:${cloth_events_version}"
        include "cloth-config:ClothConfig2:${cloth_config_version}"
        include "blue.endless:jankson:${project.jankson_version}"
    }
    modImplementation "io.github.prospector.modmenu:ModMenu:${modmenu_version}"
    compile "org.lwjgl:lwjgl-jemalloc:3.2.1"
    compileOnly "com.google.code.findbugs:jsr305:3.0.2"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")) {
                builtBy remapJar
            }
            artifact(jar) {
                builtBy jar
            }
        }
    }

    repositories {
//        if (project.hasProperty('danielshe_pass')) {
//            maven {
//                url = "http://deploy.modmuss50.me/"
//                credentials {
//                    username = "danielshe"
//                    password = project.getProperty('danielshe_pass')
//                }
//            }
//        }
    }
}