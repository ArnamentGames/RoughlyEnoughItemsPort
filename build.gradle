import net.fabricmc.loom.task.RemapJar

plugins {
    id 'fabric-loom' version '0.2.0-SNAPSHOT'
    id "com.github.johnrengelman.shadow" version "4.0.3"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "me.shedaniel"
archivesBaseName = "RoughlyEnoughItems"
version = project.modVersion

minecraft {
}

repositories {
    maven { url "https://tehnut.info/maven/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://maven.jamieswhiteshirt.com/libs-release/" }
}

processResources {
    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
    inputs.property "version", project.version
}

configurations {
    contained {
        transitive = false
    }
}

shadowJar {
    configurations = [project.configurations.contained]
}

task remapShadowJar(type: RemapJar) {
    jar = shadowJar.archivePath
}
tasks.remapShadowJar.dependsOn tasks.shadowJar

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraftVersion}"
    mappings "net.fabricmc:yarn:${project.yarnVersion}"
    modCompile "net.fabricmc:fabric-loader:${project.fabricLoaderVersion}"
    modCompile "com.jamieswhiteshirt:developer-mode:${project.developerModeVersion}"
    modCompile "info.tehnut.pluginloader:plugin-loader:${project.pluginLoaderVersion}"
    modCompile "net.fabricmc:fabric:${project.fabricVersion}"

    compile "blue.endless:jankson:${project.janksonVersion}"
    contained "blue.endless:jankson:${project.janksonVersion}"
}
