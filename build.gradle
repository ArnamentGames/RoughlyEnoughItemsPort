import java.text.SimpleDateFormat

plugins {
    id 'fabric-loom' version '0.2.5-SNAPSHOT'
    id 'maven-publish'
    id 'net.minecrell.licenser' version '0.4.1'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "RoughlyEnoughItems"
group = "me.shedaniel"

def ENV = System.getenv()
def forceVersion = ""

version = forceVersion != "" ? forceVersion : (((String) project.mod_version).contains("unstable") ? (project.mod_version + "." + buildTime()) : project.mod_version)

def includeDep = true

minecraft {
}

static def buildTime() {
    def df = new SimpleDateFormat("yyyyMMddHHmm")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

license {
    header rootProject.file('HEADER')
    include '**/*.java'
}

repositories {
    maven { url "https://tehnut.info/maven/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://dl.bintray.com/shedaniel/autoconfig1u/" }
}

processResources {
    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
    inputs.property "version", project.version
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_version}"
    modApi "net.fabricmc:fabric-loader:${project.fabricloader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${project.fabric_api}"
    modApi("me.shedaniel.cloth:cloth-events:${cloth_events_version}") {
        transitive = false
    }
    modImplementation("me.shedaniel.cloth:config-2:${cloth_config_version}") {
        transitive = false
    }
    modApi("me.sargunvohra.mcmods:autoconfig1u:${project.autoconfig1u}")
    if (includeDep) {
        include("me.shedaniel.cloth:cloth-events:${cloth_events_version}") {
            transitive = false
        }
        include("me.shedaniel.cloth:config-2:${cloth_config_version}") {
            transitive = false
        }
        include("me.sargunvohra.mcmods:autoconfig1u:${project.autoconfig1u}") {
            transitive = false
        }
    }
    modImplementation "io.github.prospector:modmenu:${modmenu_version}"
    compile "org.lwjgl:lwjgl-jemalloc:3.2.1"
    compileOnly "com.google.code.findbugs:jsr305:3.0.2"
    implementation 'org.jetbrains:annotations:15.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

task remapMavenJar(type: net.fabricmc.loom.task.RemapJarTask, dependsOn: jar) {
//    classifier = "maven"
    afterEvaluate {
        input = file("${project.buildDir}/libs/${archivesBaseName}-${version}-dev.jar")
        archiveName = "${archivesBaseName}-${version}-maven.jar"
        addNestedDependencies = false
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(file("${project.buildDir}/libs/$archivesBaseName-${version}.jar")) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
        if (project.hasProperty('danielshe_pass')) {
            maven {
                url = "http://deploy.modmuss50.me/"
                credentials {
                    username = "danielshe"
                    password = project.getProperty('danielshe_pass')
                }
            }
        }
    }
}